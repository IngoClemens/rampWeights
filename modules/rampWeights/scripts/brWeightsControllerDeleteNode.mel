//
// Procedure Name:
//      brWeightsControllerDeleteNode
//
// Description:
//      Delete the selected weights controller node and also remove any
//      weights if requested.
//
// Input Arguments:
//      type            The node type.
//      removeWeights   True, if any controlled weights should be reset.
//
// Return Value:
//      int             True, if the removal was successful.
//
global proc int brWeightsControllerDeleteNode( string $type, int $removeWeights )
{
    string $sel[] = `ls -selection -type $type`;
    if (!size($sel))
    {
        warning ("Select one or more " + $type + " nodes to delete");
        return 0;
    }

    for ($s in $sel)
    {
        int $ids[] = `getAttr -multiIndices ($s + ".weightList")`;
        for ($id in $ids)
        {
            string $conn[] = `listConnections -source 0
                                              -destination 1
                                              -plugs 1
                                              -connections 1
                                              ($s + ".weightList[" + $id + "].weights")`;

            if (size($conn) && $removeWeights)
            {
                string $items[] = stringToStringArray($conn[1], ".");
                if (`nodeType $items[0]` == "blendShape")
                {
                    disconnectAttr $conn[0] $conn[1];
                    string $cmd = "import brWeightsController;";
                    $cmd += "brWeightsController.clearWeights(\"" + $conn[1] + "\")";
                    python($cmd);
                }
                else
                    removeMultiInstance -break 1 ($items[0] + ".weightList[0]");
            }
        }

        delete $s;
    }

    return 1;
}

// ---------------------------------------------------------------------
// MIT License
//
// Copyright (c) 2021 Ingo Clemens, brave rabbit
// rampWeights and weightsServer are under the terms of the MIT License
//
// Permission is hereby granted, free of charge, to any person obtaining
// a copy of this software and associated documentation files (the
// "Software"), to deal in the Software without restriction, including
// without limitation the rights to use, copy, modify, merge, publish,
// distribute, sublicense, and/or sell copies of the Software, and to
// permit persons to whom the Software is furnished to do so, subject to
// the following conditions:
//
// The above copyright notice and this permission notice shall be
// included in all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
// EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
// MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
// IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
// CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
// TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
// SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//
// Author: Ingo Clemens    www.braverabbit.com
// ---------------------------------------------------------------------
