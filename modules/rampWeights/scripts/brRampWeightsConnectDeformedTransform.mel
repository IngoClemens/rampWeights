//
// Procedure Name:
//      getTransform
//
// Description:
//      Return the transform node of the deformed geometry
//
// Input Arguments:
//      node            The name of the deformer node.
//
// Return Value:
//      string          The name of the transform node.
//
proc string getTransform( string $node )
{
    string $hist[] = `listHistory $node`;
    string $transform[];
    for ($h in $hist)
    {
        string $items[] = `ls -shapes -intermediateObjects $h`;
        if (size($items))
        {
            $transform = `listRelatives -parent $items[0]`;
            break;
        }
    }
    return $transform[0];
}


//
// Procedure Name:
//      brRampWeightsConnectDeformedTransform
//
// Description:
//      Connect the world matrix of the deformed mesh of the ramp
//      weights node of it's not already connected.
//
// Input Arguments:
//      node            The name of the ramp weights node.
//      deformer        The name of the deformer.
//
// Return Value:
//      None
//
global proc brRampWeightsConnectDeformedTransform( string $node, string $deformer )
{
    // Before connecting the transform of the deformed mesh check if
    // it's already connected. This is much cleaner than forcing the
    // connection and deal with an existing connection.
    string $transform = getTransform($deformer);
    string $conn = `connectionInfo -sourceFromDestination ($node + ".worldMatrix")`;
    int $connect = 1;
    if ($conn != "")
    {
        string $items[] = stringToStringArray($conn, ".");
        if ($items[0] == $transform)
            $connect = 0;
    }
    if ($connect)
        connectAttr -force ($transform + ".worldMatrix[0]") ($node + ".worldMatrix");
}

// ---------------------------------------------------------------------
// MIT License
//
// Copyright (c) 2019 Ingo Clemens, brave rabbit
// rampWeights is under the terms of the MIT License
//
// Permission is hereby granted, free of charge, to any person obtaining
// a copy of this software and associated documentation files (the
// "Software"), to deal in the Software without restriction, including
// without limitation the rights to use, copy, modify, merge, publish,
// distribute, sublicense, and/or sell copies of the Software, and to
// permit persons to whom the Software is furnished to do so, subject to
// the following conditions:
//
// The above copyright notice and this permission notice shall be
// included in all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
// EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
// MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
// IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
// CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
// TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
// SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//
// Author: Ingo Clemens    www.braverabbit.com
// ---------------------------------------------------------------------
