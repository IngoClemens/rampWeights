//
// Procedure Name:
//      getNextIndex
//
// Description:
//      Return the next available weight list index of the weights
//      server node which is not used or connected.
//
// Input Arguments:
//      node            The name of the weights controller node.
//
// Return Value:
//      int             The next available weight list index.
//
proc int getNextIndex( string $node )
{
    int $index = -1;

    string $attr = $node + ".weightList";
    int $ids[] = `getAttr -multiIndices $attr`;
    for ($i = 0; $i < size($ids); $i ++)
    {
        if ($index == -1)
        {
            string $conn[] = `connectionInfo -destinationFromSource ($attr + "[" + $i + "].weights")`;
            if (!size($conn))
            {
                $index = $i;
                break;
            }
        }
    }
    if ($index == -1)
        $index = size($ids);

    return $index;
}


//
// Procedure Name:
//      brWeightsControllerConnectDeformer
//
// Description:
//      Connect the given deformer with the weights controller node.
//
// Input Arguments:
//      node            The name of the weights controller node.
//      deformer        The name of the deformer to be controlled.
//      index           The index of the blend shape target if any.
//
// Return Value:
//      int             The weight list index the deformer is connected
//                      to.
//
global proc int brWeightsControllerConnectDeformer( string $node, string $deformer, int $index )
{
    int $nextIndex = getNextIndex($node);
    string $srcAttr = $node + ".weightList[" + $nextIndex + "].weights";
    string $destAttr = $deformer + ".weightList[0].weights";

    if (`nodeType $deformer` == "blendShape")
        $destAttr = $deformer + ".inputTarget[0].inputTargetGroup[" + $index + "].targetWeights";

    brWeightsControllerConnectAttr($srcAttr, $destAttr);

    return $nextIndex;
}

// ---------------------------------------------------------------------
// MIT License
//
// Copyright (c) 2021 Ingo Clemens, brave rabbit
// rampWeights and weightsServer are under the terms of the MIT License
//
// Permission is hereby granted, free of charge, to any person obtaining
// a copy of this software and associated documentation files (the
// "Software"), to deal in the Software without restriction, including
// without limitation the rights to use, copy, modify, merge, publish,
// distribute, sublicense, and/or sell copies of the Software, and to
// permit persons to whom the Software is furnished to do so, subject to
// the following conditions:
//
// The above copyright notice and this permission notice shall be
// included in all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
// EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
// MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
// IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
// CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
// TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
// SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//
// Author: Ingo Clemens    www.braverabbit.com
// ---------------------------------------------------------------------
